ext {
    hibernateVersion = '5.1.1.Final'
}

buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }

	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

allprojects {
}

subprojects() {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-security')
        compile('org.projectlombok:lombok')
        compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.6')

        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('org.springframework.security:spring-security-test:4.2.1.RELEASE')
        testCompile('org.assertj:assertj-core:3.6.2')
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.enabled true
        }

        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it,
                        exclude: ['**/Cafe*Application.class',
                                  '**/**Id.class'])
            })
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Werror"
    }
}

project(':shared') {
    jar {
        baseName = 'cafe-shared'
        version = '0.0.1-SNAPSHOT'
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-data-jpa')
        compile("org.hibernate:hibernate-java8:${hibernateVersion}")

        runtime('com.h2database:h2')
        runtime('mysql:mysql-connector-java')
    }

}

project(':web') {
    jar {
        baseName = 'cafe-web'
        version = '0.0.1-SNAPSHOT'
    }

    dependencies {
        compile project(':shared')

        compile("org.springframework.boot:spring-boot-starter-thymeleaf")
        compile("org.springframework.boot:spring-boot-devtools")

        compile('org.thymeleaf.extras:thymeleaf-extras-java8time:2.1.0.RELEASE')
        compile('org.thymeleaf.extras:thymeleaf-extras-springsecurity4:2.1.3.RELEASE')

        compile("org.hibernate:hibernate-java8:${hibernateVersion}")
        runtime('com.h2database:h2')
        runtime('mysql:mysql-connector-java')

        runtime("org.webjars:webjars-locator:0.30")
        runtime("org.webjars:angularjs:1.6.1")
        runtime("org.webjars:bootstrap:3.3.7")
    }

    bootRun {
        addResources = true
    }
}